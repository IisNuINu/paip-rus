# Глава 4
## GPS:  Универсальный решатель проблем(General problem Solver)

> *Сейчас в мире есть машины, которые думают.*

> -Herbert Simon

> Nobel Prize-winning Al researcher

Универсальный решатель проблем, разработанный в 1957 году Аланом Ньюэллом и Гербертом Саймоном, воплощал грандиозное видение: единую компьютерную программу, которая могла бы решить любую проблему, получив соответствующее описание проблемы.
GPS вызвал настоящий ажиотаж, когда он был представлен, и некоторые люди в ИИ чувствовали, что он открыл грандиозную новую эру интеллектуальных машин.
Саймон зашел так далеко, что сделал это заявление о своем творении:

> *В мои намерения не входит удивлять или шокировать вас.... Но самый простой способ подвести итог-это сказать, что сейчас в мире существуют машины, которые думают, учатся и творят.*
>*Более того, их способность делать эти вещи будет быстро возрастать до тех пор, пока - в обозримом будущем - диапазон проблем, с которыми они могут справиться, не станет соразмерным с диапазоном, к которому может быть применен человеческий разум.*

Хотя GPS никогда не соответствовала этим преувеличенным заявлениям, она все же является важной программой по историческим причинам.
Это была первая программа, которая отделила свою стратегию решения проблем от знания конкретных проблем, и она стимулировала дальнейшие исследования в области решения проблем.
По всем этим причинам она является подходящим для изучения объектом.

Оригинальная программа GPS имела ряд незначительных особенностей, которые делали ее довольно сложной.
Кроме того, она была написана на устаревшем низкоуровневом языке IPL, что добавляло ей неоправданной сложности.
На самом деле запутанная природа IPL, вероятно, была важной причиной для грандиозных заявлений о GPS.
Если программа была настолько сложной, она должна была сделать что-то важное.
Мы будем игнорировать некоторые тонкости оригинальной программы, и мы будем использовать Common Lisp, гораздо более ясный язык, чем IPL.
Результатом будет версия GPS, которая довольно проста, но иллюстрирует некоторые важные моменты ИИ.

С одной стороны, эта глава посвящена GPS.
Но на другом уровне речь идет о процессе разработки компьютерной программы искусственного интеллекта.
Мы выделяем пять этапов в разработке программы.
Во-первых, это описание проблемы, которое представляет собой приблизительное представление - обычно написанное простым текстом - о том, что мы хотим сделать.
Во-вторых, это спецификация программы, где мы заново описываем проблему в терминах, которые ближе к вычислительной процедуре.
Третий этап-это реализация программы на таком языке программирования, как Common Lisp, Четвертый-тестирование, и Пятый-отладка и анализ.
Границы между этими стадиями изменчивы, и стадии не обязательно завершаются в указанном порядке.
Проблемы на любом этапе могут привести к изменению предыдущего этапа или даже к полному перепроектированию или отказу от проекта.
Программист может предпочесть завершить только частичное описание или спецификацию, перейти непосредственно к реализации и тестированию, а затем вернуться к завершению спецификации на основе улучшевшегося понимания.

Мы пройдем по всем пяти этапам разработки наших версий GPS, надеясь, что читатель лучше поймет GPS, а также лучше поймет, как написать собственную программу.
Подводя итог, можно выделить пять этапов проекта по программированию искусственного интеллекта:

1.  **Опишите** проблему в расплывчатых терминах

2.  **Определите** задачу в алгоритмических терминах

3.  **Реализуйте** задачу на языке программирования

4.  **Проверьте** программу на репрезентативных примерах

5.  **Отладьте** и **проанализируйте** полученную программу и повторите процесс

## 4.1 Этап 1: Описание

В качестве описания нашей проблемы мы начнем с цитаты из книги Ньюэлла и Саймона 1972 года: *Решение Человеческих Проблем*

> *Основные методы GPS совместно воплощают эвристику анализа конечных целей.
Анализ конечных целей характеризуется следующим типом аргументов здравого смысла:*

*Я хочу отвести своего сына в детский сад.
Какая разница между тем, что у меня есть, и тем, что я хочу?
Одна из дистанций.
Что изменяет расстояние?
Мой автомобиль.
Мой автомобиль не работает.
Что нужно для того, чтобы он заработал?
Новая батарея.
У кого есть новые батареи?
В Мастерской по ремонту автомобилей.
Я хочу, чтобы ремонтная мастерская поставила новую батарею, но мастерская не знает, что она мне нужна.
В чем же затруднение?
Один из способов общения.
Что позволяет общаться?
Телефон... и так далее.*

> *Тип анализа, классифицирующий вещи в терминах функций, которые они выполняют и который колеблется(переключается) между целями, требуемыми функциями и средствами, которые их выполняют, формирует базовую систему эвристики GPS.*


Конечно, этот вид анализа не совсем новый.
Теория анализа конечных целей была довольно изящно изложена Аристотелем 2300 годами ранее в главе под названием "Природа размышлений и ее объекты" *Никомаховой этики* (Книга III. 3,1112b)

> *Мы размышляем не о целях, а о средствах.
Ибо врач не обдумывает, будет ли он исцелять, или оратор, будет ли он убеждать, или государственный деятель, будет ли он создавать закон и порядок, и никто другой не обдумывает об этом.
Они предполагают цель и рассматривают, как и какими средствами она достигается; и если кажется, что она может быть достигнута несколькими средствами, они рассматривают, с помощью которых она наиболее легко и наилучшим образом достигается, в то время как если она достигается только одним, они рассматривают, как она будет им достигнута и какими дополнительными средствами это средство будет достигнуто, пока они не придут к первой причине, которая в порядке открытия является последней... и то, что является последним в порядке анализа, кажется первым в порядке установления.
И если мы приходим к невозможности, мы отказываемся от поиска, например, если нам нужны деньги, а их нельзя получить; но если что-то кажется возможным, мы пытаемся это сделать.*

Учитывая это описание теории решения проблем, как мы должны писать программу?
Сначала мы пытаемся более полно понять процедуру, изложенную в кавычках.
Основная идея состоит в том, чтобы решить проблему, используя процесс, называемый анализом конечных целей, где проблема формулируется в терминах того, что нам надо чтобы это произошло.
В примере Ньюэлла и Саймона проблема состоит в том, чтобы доставить ребенка в школу, но в целом мы хотели бы, чтобы программа могла решать широкий класс проблем.
Мы можем решить проблему, если найдем способ устранить «разницу между тем, что у меня есть, и тем, что я хочу». Например, если у меня есть ребенок дома, а то, что я хочу, это ребенок в школе, то поездка может быть решением, потому что мы знаем, что езда на машине ведет к изменению местоположения.
Нам следует помнить, что использование анализа конечных целей - это наш выбор: также можно начать с текущей ситуации и идти к цели или использовать смесь различных поисковых стратегий.

Некоторые действия требуют решения *предварительных условий* как подзадач.
Прежде чем мы сможем управлять автомобилем, нам нужно решить подзадачу приведения автомобиля в рабочее состояние.
Возможно, что машина уже работает, и в этом случае нам ничего не нужно делать, чтобы решить эту подзадачу.
Таким образом, проблема решается либо путем принятия соответствующего действия непосредственно, либо путем предварительного решения для предварительных условий для выполнения этого действия, а затем уже и выполнения самого действия.
Понятно, что нам потребуется некоторое описание допустимых действий, а также их предпосылок и последствий(результатов/effects).
Нам также необходимо разработать определение уместности.
Однако если мы сможем лучше определить эти понятия, то, похоже, нам не понадобятся никакие новые понятия.
Таким образом, мы произвольно решаем, что описание проблемы завершено и перейдем к спецификации проблемы.

## 4.2 Этап 2: Спецификация(Детализация)

На данный момент у нас есть представление, правда смутное, о том, что значит решать проблему в `GPS`. Мы можем уточнить эти понятия в представления, которые ближе к Lisp следующим образом:

*   Мы можем представить текущее состояние мира как "то, что у меня есть" и состояние цели, как "то, что я хочу", как набор условий.
Common Lisp не имеет типа данных для множеств, но у него есть списки, которые можно использовать для реализации множеств.
Каждое условие может быть представлено символом.
Таким образом, типичной целью может быть список двух состояний (`rich famous`)/(`богатый знаменитый`), а типичным текущим состоянием может быть (`unknown poor`)/(`неизвестный бедный`).

*   Нам нужен список допустимых операторов.
Этот список будет постоянным  течение проблемы или даже серии проблем, но мы хотим иметь возможность изменять его и искать решение в новой проблемной области.

*  Оператор может быть представлен в виде структуры, состоящей из действия(action), списка предварительных условий(preconditions) и списка эффектов(effects).
Мы можем установить ограничения на виды возможных эффектов, сказав, что эффект либо добавляет, либо удаляет условие из текущего состояния.
Таким образом, список эффектов можно разделить на список добавления(add-list) и список удаления(delete-list).
Это был подход, принятый в Stripes [1](#fn0010) реализации GPS, который мы фактически реконструируем в этой главе.
Оригинальный GPS позволял большую гибкость в спецификации/определении эффектов, но гибкость приводит к неэффективности.

*   Полная задача описывается для GPS в терминах начального состояния(starting state), целевого состояния(goal state) и набора/множества известных операторов(known operators).
Таким образом, GPS будет функцией трех аргументов.
Например, пример вызова может быть:  `(GPS  '(unknown poor)  '(rich famous) list-of-ops)`,  другими словами, начиная с состояния быть бедным(poor) и неизвестным(unknown), надо достичь состояния быть богатым(rich) и знаменитым(famous), используя любую комбинацию известных операторов(list-of-ops).
GPS должен возвращать истинное значение только в том случае, если он решает проблему, и он должен напечатать запись о предпринятых действиях.
Самый простой подход состоит в том, чтобы пройти через условия к состоянию цели по одному(условию цели) за раз и попытаться достичь каждого из них.
Если все они могут быть достигнуты, то проблема решена.

*   Одно условие цели может быть достигнуто двумя способами.
Если оно(целевое условие) уже находится в текущем состоянии, то цель тривиально достигается без каких-либо усилий.
В противном случае мы должны найти какой-то подходящий оператор(добавляющий это условие) и попытаться применить его.

*   Оператор уместен/применим, если одним из эффектов оператора является добавление рассматриваемой цели в текущее состояние; другими словами, если цель находится в списке добавлений(add-list) оператора.

*  Мы можем применить оператор, если мы можем достичь всех предварительных условий.
Но это легко, потому что мы только что определили понятие достижения цели в предыдущем абзаце.
После выполнения предварительных условий применение оператора означает выполнение действия и обновление текущего состояния в терминах  операторов списка добавлений(add-list) и списка удалений(delete-list).
Поскольку наша программа-это всего лишь симуляция, а не фактическое вождение автомобиля или набора номера телефона, мы должны довольствоваться просто распечаткой действия, а не выполнением каких-либо реальных действий.

## 4.3 Этап 3: Реализация

Спецификация достаточно полна, чтобы привести непосредственно к полной программе Common Lisp.
[Рисунок 4.1](#f0010) суммирует переменные, типы данных и функции, составляющие программу GPS, а также некоторые из функций Common Lisp, используемых для ее реализации.

| Символ             | Использование                                                |
| ------             | ---                                                          |
|                    | **Функция верхнего уровня**                                  |
| `GPS`              | Решает задачу из сост., используя список операторов.         |
|                    | **Специальные переменные**                                   |
| `*state*`          | Текущее состояние, это список условий.                       |
| `*ops*`            | Список доступных операций.                                   |
|                    | **Типы Данных**                                              |
| `op`               | Операции с preconds, add-list и del-list.                    |
|                    | **Функции**                                                  |
| `achieve`          | Достижение индивидуальной цели.                              |
| `appropriate-p`    | Решает, подходит ли оператор для достижения цели.            |
| `apply-op`         | Применяет оператор к текущему состоянию.                     |
|                    | **Выбранные Функции Common Lisp**                            |
| `member`           | Проверяет содержиться ли элемент в списке. (p.78)            |
| `set-difference`   | Все элементы из одного можества, которых нет в другом.       |
| `union`            | Все элементы из обоих мноожеств                              |
| `every`            | Проверяет, проходит ли тест каждый элемент списка. (p. 62)   |
| `some`             | Проверяет, проходит ли тест какой-либо элемент списка.       |
|                    | **Ранее Определенные Функции**                             |
| `find-all`         | Список всех совпадающих элементов. (p. 101)                    |

Вот полная программа GPS сама по себе:

```lisp
(defvar *state* nil "The current state: a list of conditions.")

(defvar *ops* nil "A list of available operators.")

(defstruct op "An operation"
  (action nil) (preconds nil) (add-list nil) (del-list nil))

(defun GPS (*state* goals *ops*)
  "General Problem Solver: achieve all goals using *ops*."
  (if (every #'achieve goals) 'solved))

(defun achieve (goal)
  "A goal is achieved if it already holds,
  or if there is an appropriate op for it that is applicable."
  (or (member goal *state*)
    (some #'apply-op
      (find-all goal *ops* :test #'appropriate-p))))

(defun appropriate-p (goal op)
  "An op is appropriate to a goal if it is in its add list."
  (member goal (op-add-list op)))

(defun apply-op (op)
  "Print a message and update *state* if op is applicable."
  (when (every #'achieve (op-preconds op))
    (print (list 'executing (op-action op)))
    (setf *state* (set-difference *state* (op-del-list op)))
    (setf *state* (union *state* (op-add-list op)))
  t))
```
Мы видим, что программа состоит из семи определений.
Они соответствуют семи пунктам приведенной выше спецификации.
В общем, вы не должны ожидать такого идеального соответствия между спецификацией и реализацией.
Существует две формы `defvar`, одна `defstruct` и четыре формы `defun`.
Это формы Lisp для определения переменных, структур и функций соответственно.
Они являются наиболее распространенными формами верхнего уровня в Lisp, но в них нет ничего волшебного; это просто специальные формы, которые имеют побочный эффект добавления новых определений в среду Lisp.

Две формы `defvar`, повторенные ниже, объявляют специальные переменные с именами  `*state*` и `*ops*,` к которым затем можно получить доступ из любой точки программы.

```lisp
(defvar *state* nil "The current state: a list of conditions.")
(defvar *ops* nil "A list of available operators.")
```
Форма `defstruct` определяет структуру называемую `op`, которая имеет слоты под названием `action`, `preconds`, `add-list` и `del-list`(действие, предварительные условия, списко добавления и список удаления).
Структуры в Common Lisp подобны структурам в Си или записям в Pascal.
`defstruct` автоматически определяет функцию конструктора, которая называется `make-op` и функцию доступа для каждого слота структуры.
Функции доступа называются `op-action`, `op-preconds`, `op-add-list` и `op-del-list`.
В `defstruct` также определяется функция копирования `copy-op`, предикат `op-p` и определения `setf` для изменения каждого слота.
Ни одина из них не используется в программе GPS.
Грубо говоря, это как если бы форма `defstruct`

```lisp
(defstruct op "An operation"
  (action nil) (preconds nil) (add-list nil) (del-list nil))
```
расширялась бы в следующие определения:

```lisp
(defun make-op (&key action preconds add-list del-list)
  (vector 'op action preconds add-list del-list))

(defun op-action (op) (elt op 1))

(defun op-preconds (op) (elt op 2))

(defun op-add-list (op) (elt op 3))

(defun op-del-list (op) (elt op 4))

(defun copy-op (op) (copy-seq op))

(defun op-p (op)
  (and (vectorp op) (eq (elt op 0) 'op)))

(setf (documentation 'op 'structure) "An operation")
```
Далее в программе GPS идут четыре определения функций.
Основная функция `GPS`, принимает три аргумента.
Первый - это текущее состояние мира, второй - целевое состояние, а третий - список допустимых операций(операторов).
Тело функции просто говорит, что если мы сможем достичь каждой из поставленных целей, то проблема будет решена.
Неназванная альтернатива заключается в том, что в противном случае проблема не будет решена.

Функция achieve принимает в качестве аргумента единственную цель(goal).
Функция работает успешно, если эта цель уже верна(достигнута) в текущем состоянии (в этом случае нам ничего не нужно делать) или если мы можем применить соответствующий оператор.
Это достигается путем предварительного построения списка подходящих операторов, а затем тестирования каждого по очереди, пока один из них не сможет быть применен.
`achieve` вызывает `find-all`, который мы определили на [page 101](B9780080571157500030.xhtml#p101).
При таком использовании `find-all` возвращает список операторов, соответствующих текущей цели, в соответствии с предикатом `appropriate-p`.

Функция `appropriate-p` проверяет, подходит ли оператор для достижения цели.
(Это следует из соглашения об именовании Lisp, что предикаты заканчиваются на `-p`.)

Наконец, функция `apply-op` говорит, что если мы можем достичь всех предварительных условий для соответствующего оператора, то мы можем применить оператор.
Это включает в себя печать сообщения об этом эффекте и изменение состояния мира путем удаления того, что было в списке удаления(delete-list), и добавления того, что было в списке добавления(add-list).
`apply-op` также является предикатом; он возвращает `t` только тогда, когда оператор может быть применен.

## 4.4 Этап 4: Тестирование

В этом разделе будет определен список операторов, применимых к домену "вождение в детский сад", и показано, как ставить и решать некоторые проблемы в этом домене.
Во-первых, нам нужно построить список операторов для домена.
Форма `defstruct` для типа `op` автоматически определяет функцию `make-op`, которую можно использовать следующим образом:

```lisp
(make-op :action 'drive-son-to-school
    :preconds '(son-at-home car-works)
    :add-list '(son-at-school)
    :del-list '(son-at-home))
```
Это выражение возвращает оператор, чьим действием является символ `drive-son-to-school` и чьи предварительные условия, список добавления(add-list) и список удаления(delete-list) являются указанными списками.
Цель этого оператора заключается в том, что всякий раз, когда сын находится дома(son-at-home) и машина работает(car-works), можно применить `drive-son-to-school`("отвезти сына в школу"), изменяя состояние, удалив тот факт, что сын находится дома(son-at-home), и добавив тот факт, что он находится в школе(son-at-school).

Следует отметить, что использование длинных атомов с разделлительными дефисами, таких как `son-at-home`, является полезным подходом только для очень простых примеров, подобных этому.
Более точное представление разбило бы атом на его компоненты: возможно (`at son home`)..
Проблема атомного подхода - это комбинаторная проблема.
Если есть 10 предикатов (таких как, `at`) и 10 людей или объектов, то будет 10 x 10 x 10 = 1000 возможных атомов разделенных дефисом, но только 20 компонентов.
Очевидно, что было бы проще описать компоненты.
В этой главе мы придерживаемся разделенных дефисами атомов, потому что это проще, и нам не нужно описывать весь мир.
В последующих главах представление знаний рассматривается более серьезно.

Используя этот оператор в качестве модели, мы можем определить другие операторы, соответствующие цитате Ньюэлла и Саймона на [page 109](B9780080571157500042.в XHTML#р109).
Там будет оператор для установки батареи, сообщающий в ремонтную мастерскую о проблеме и звонящий в мастерскую.
Мы можем заполнить "и так далее", добавив операторы для поиска номера телефона магазина и для передаче магазину денег:

```lisp
(defparameter *school-ops*
  (list
    (make-op :action 'drive-son-to-school
      :preconds '(son-at-home car-works)
      :add-list '(son-at-school)
      :del-list '(son-at-home))
    (make-op :action 'shop-installs-battery
      :preconds '(car-needs-battery shop-knows-problem shop-has-money)
      :add-list '(car-works))
    (make-op :action 'tell-shop-problem
      :preconds '(in-communication-with-shop)
      :add-list '(shop-knows-problem))
    (make-op :action 'telephone-shop
      :preconds '(know-phone-number)
      :add-list '(in-communication-with-shop))
    (make-op :action 'look-up-number
      :preconds '(have-phone-book)
      :add-list '(know-phone-number))
    (make-op :action 'give-shop-money
      :preconds '(have-money)
      :add-list '(shop-has-money)
      :del-list '(have-money))))
```
Следующий шаг-поставить некоторые проблемы перед GPS и изучить их решения.
Ниже приведены три примера проблем.
В каждом случае цель одна и та же: достичь единственного условия `son-at-school`(сын-в-школе).
Список доступных операторов также одинаков в каждой задаче; разница заключается в исходном состоянии.
Каждый из трех примеров состоит из подсказки ">", которая печатается системой Lisp, а затем следует вызов GPS  " ( `gps`... )", который набирается пользователем, а затем выводится  программой "(`EXECUTING`...)" и, наконец, результат вызова функции, который может быть либо `SOLVED`(решен), либо `NIL`(не решен).

```lisp
> (gps '(son-at-home car-needs-battery have-money have-phone-book)
    '(son-at-school)
    *school-ops*)
(EXECUTING LOOK-UP-NUMBER)
(EXECUTING TELEPHONE-SHOP)
(EXECUTING TELL-SHOP-PROBLEM)
(EXECUTING GIVE-SHOP-MONEY)
(EXECUTING SHOP-INSTALLS-BATTERY)
(EXECUTING DRIVE-SON-TO-SCHOOL)
SOLVED
> (gps '(son-at-home car-needs-battery have-money)
    '(son-at-school)
    *school-ops*)
NIL
> (gps '(son-at-home car-works)
    '(son-at-school)
    *school-ops*)
(EXECUTING DRIVE-SON-TO-SCHOOL)
SOLVED
```
Во всех трех примерах цель состоит в том, чтобы  `son-at-school`(сын был в школе).
Единственный оператор, у которого есть `son-at-school` в своем списке добавлениядополнений(add-list) -это `drive-son-to-school`(отвезти/переместить сына в школу), поэтому GPS изначально выбирает этот оператор.
Прежде чем он сможет выполнить этот оператор, GPS должен решить для себя предварительные условия этого оператора.
В первом примере программа работает в обратном направлении через операторы `shop-installs-battery`, `give-shop-money`, `tell-shop-problem` и `telephone-shop` к `look-up-number`, который не имеет никаких значимых предварительных условий.
Таким образом, действие `look-up-number`(поиск номера) может быть выполнено, и программа переходит к другим действиям.
Как сказал Аристотель: "то, что появляется последним в порядке анализа, будет первым в порядке становления(выполнения)."

Второй пример начинается точно так же, но оператор  `look-up-number` терпит неудачу, потому что его предварительное условие `have-phone-book`, не может быть достигнуто.
Знание номера телефона является обязательным условием, прямо или косвенно, для всех операторов, поэтому никаких действий не предпринимается и GPS возвращает `NIL`.

Наконец, третий пример является гораздо более прямым; начальное состояние указывает, что автомобиль работает, поэтому оператор вождения может быть применен немедленно.

## 4.5 Стадия 5: анализ, или "мы солгали о G(Общности решателя проблем)"

В следующих разделах мы рассмотрим вопрос о том, насколько общим является этот общий решатель проблем.
Следующие четыре раздела указывают на ограничения нашей версии GPS, и мы покажем, как исправить эти ограничения во второй версии программы.

Можно было бы спросить, не является ли "ограничения" просто эвфемизмом(хорошим термином) для "ошибок".- Мы "расширяем" программу или "корректируем" ее?
На этот вопрос нет четких ответов, потому что мы никогда не настаивали на однозначном описании или спецификации проблемы.
Программирование ИИ в значительной степени является исследовательским программированием; цель часто состоит в том, чтобы узнать больше о проблемной области, а не соответствовать четко определенной спецификации.
Это противоречит более традиционному представлению о программировании, где задача полностью определяется до того, как будет написана первая строка кода.

## 4.6 Проблема бега вокруг блока.

Представление оператора "вождение из дома в школу" простое: предварительное условие и список удалений включают в себя пребывание дома, а список добавлений включает в себя пребывание в школе.
Но предположим, что мы хотим изобразить "бег вокруг блока"."Не будет никакого чистого изменения местоположения, так означает ли это, что не будет никакого списка добавления или удаления?
Если бы это было так, то не было бы никакой причины когда-либо применять какой-либо оператор.
Возможно, список дополнений должен содержать что-то вроде "немного потренировался" или "чувствую усталость", или что-то более общее, например "опыт бега вокруг блока".- Мы вернемся к этому вопросу позже.

## 4.7 Проблема цели - сломаный брат

Подумайте о том, чтобы не только доставить ребенка в школу, но и иметь немного денег, чтобы использовать их до конца дня.
GPS может легко решить эту проблему из следующего начального условия:

```lisp
(gps '(son-at-home have-money car-works)
    '(have-money son-at-school)
    *school-ops*)
(EXECUTING DRIVE-SON-TO-SCHOOL)
SOLVED
```
Однако в следующем примере GPS неверно сообщает об успехе, когда на самом деле он потратил деньги на батарею.

```lisp
> (gps '(son-at-home car-needs-battery have-money have-phone-book)
    '(have-money son-at-school)
    *school-ops*)
(EXECUTING LOOK-UP-NUMBER)
(EXECUTING TELEPHONE-SHOP)
(EXECUTING TELL-SHOP-PROBLEM)
(EXECUTING GIVE-SHOP-MONEY)
(EXECUTING SHOP-INSTALLS-BATTERY)
(EXECUTING DRIVE-SON-TO-SCHOOL)
SOLVED
```
"Ошибка" заключается в том, что GPS использует выражение (`every #'achieve goals`) для достижения набора целей.
Если это выражение возвращает true, это означает, что каждая из целей была достигнута последовательно, но это не значит, что все они по-прежнему верны в конце.
Иными словами, цель (`have-money son-at-school`), который у нас означает "оказаться в состоянии, когда есть деньги и сын в школе - истина" было истолковано GPS так: "вначале достичь"   `have-money`, а затем достичь   `son-at-school`. Иногда достижение одной цели может отменить другую, ранее достигнутую цель.
Мы назовем это проблемой "предпосылок для достижения братской цели".[2](#fn0015) то есть `have-money` и `son-at-school` -это родственные цели, одна из предпосылок для плана `son-at-school` - это работающая машина(`car-works`), и достижение этой цели разрушает цель `have-money`.

Изменение программы для распознавания проблемы "предпосылки повреждение брата цели" является простым.
Во-первых, обратите внимание, что мы вызываем (`every #``achieve` *something*) дважды в рамках программы, поэтому давайте заменим эти две формы на ( `achieve-all` *something*).
Затем мы можем определить `achieve-all` следующим образом:

```lisp
(defun achieve-all (goals)
  "Try to achieve each goal, then make sure they still hold."
  (and (every #'achieve goals) (subsetp goals *state*)))
```
Функция Common Lisp subsetp  возвращает true, если ее первый аргумент является подмножеством его вторго аргумента. В `achieve-all`, она возвращает значение true, если каждая из целей все еще  находится в текущем состоянии после достижения всех целей. 
Это то, что мы хотели проверить.

Введение `achieve-all` предотвращает возврат из GPS истины, когда одина из целей повреждена, но это не заставляет GPS перепланировать и попытаться восстановиться из за поврежденной цели.
Мы не будем рассматривать эту возможность сейчас, но мы ее рассмотрим снова в разделе о домене мира блоков, который был основным примером Суссмана.


## 4.8 Прыжок перед тем как вы увидели проблему(прыгнуть в слепую).

Еще один способ решить проблему "предпослыки повреждения брата цели" - это просто более внимательно отнестись к порядку целей в списке целей.
Если мы хотим отправить ребенка в школу и еще иметь немного денег, почему бы просто не сказать (`son-at-school have-money`), а не (`have-money son-at-school`)?
Давайте посмотрим, что произойдет когда мы попробуем это сделать:

```lisp
> (gps '(son-at-home car-needs-battery have-money have-phone-book)
    '(son-at-school have-money)
    *school-ops*)
(EXECUTING LOOK-UP-NUMBER)
(EXECUTING TELEPHONE-SHOP)
(EXECUTING TELL-SHOP-PROBLEM)
(EXECUTING GIVE-SHOP-MONEY)
(EXECUTING SHOP-INSTALLS-BATTERY)
(EXECUTING DRIVE-SON-TO-SCHOOL)
NIL
```
GPS возвращает nil, отражая тот факт, что цель не может быть достигнута, но только после того, как выполнены все действия, вплоть до поездки в школу, включительно.
Я называю это "прыжком перед тем как проблема будет видна", потому что если бы вы попросили программу решить проблему для двух целей `(jump-off-cliff land-safely)`, она бы вначале прыгнула и только потом обнаружила, что в ней нет оператора, чтобы безопасно приземлиться.
Это далеко не благоразумное поведение.

Проблема возникает потому, что планирование и исполнение чередуются. Как только предварительные условия для оператора достигнуты, предпринимается дейтсвие-и состояние `*state*` бесповоротно изменяется, даже если это действие, в конечном итоге, может завести в тупк.
Альтернативой было бы заменить одно глобальное состояние `*state*` различными локальными переменными состояния, так что для каждого нового состояния создавалась бы новая переменная.
как мы увидим в следующем разделе, эта альтернатива хороша и по другой, независимой причине.

## 4.9 Проблема рекурсивности подцели

В нашем моделируемом мире с детским садом есть только один способ узнать номер телефона: посмотреть его в телефонной книге.
Предположим, мы хотим добавить оператор, позволяющий узнать номер телефона, спросив кого-либо.
Конечно, для того чтобы спросить кого-то о чем-то, вы должны общаться с ним или ней.
Оператор для запроса телефонного номера(asking-for-a-phone-number) может быть реализован следующим образом:

```lisp
(push (make-op :action 'ask-phone-number
      :preconds '(in-communication-with-shop)
      :add-list '(know-phone-number))
    *school-ops*)
```
(Специальная форма ( `push`*item list*) помещает элемент в начало списка; это выражение эквивалентно (setf *list* (`cons`*item list*) ) в простом случае.) К сожалению, иногда случается нечто не ожиданное, когда мы пытаемся решить, казалось бы простые, проблемы с этим новым набором(множеством) операторов. 
Обратите внимание на следующее:

```lisp
> (gps '(son-at-home car-needs-battery have-money)
    '(son-at-school)
    *school-ops*)
>>TRAP 14877 (SYSTEM:PDL-OVERFLOW EH: :REGULAR)
The regular push-down list has overflown.
While in the function ACHIEVE <- EVERY <- REMOVE
```
Сообщение об ошибке (которое будет варьироваться от одной реализации Common Lisp к другой) означает, что было сделано слишком много рекурсивно вложенных вызовов функций.
Это указывает либо на очень сложную проблему, либо, чаще всего, на ошибку в программе, которая ведет к бесконечной рекурсси.
Один из способов попытаться увидеть причину ошибки - это трассировать соответствующую функцию, такую как `achieve`:

`> (trace achieve)`=> `(ACHIEVE)`

```lisp
> (gps '(son-at-home car-needs-battery have-money)
    '(son-at-school)
    *school-ops*)
(1 ENTER ACHIEVE: SON-AT-SCHOOL)
  (2 ENTER ACHIEVE: SON-AT-HOME)
  (2 EXIT ACHIEVE: (SON-AT-HOME CAR-NEEDS-BATTERY HAVE-MONEY))
  (2 ENTER ACHIEVE: CAR-WORKS)
    (3 ENTER ACHIEVE: CAR-NEEDS-BATTERY)
    (3 EXIT ACHIEVE: (CAR-NEEDS-BATTERY HAVE-MONEY))
    (3 ENTER ACHIEVE: SHOP-KNOWS-PROBLEM)
      (4 ENTER ACHIEVE: IN-COMMUNICATION-WITH-SHOP)
        (5 ENTER ACHIEVE: KNOW-PHONE-NUMBER)
          (6 ENTER ACHIEVE: IN-COMMUNICATION-WITH-SHOP)
            (7 ENTER ACHIEVE: KNOW-PHONE-NUMBER)
              (8 ENTER ACHIEVE: IN-COMMUNICATION-WITH-SHOP)
                (9 ENTER ACHIEVE: KNOW-PHONE-NUMBER)
```
Выходные данные трассировки trace дают необходимые подсказки.
Ньюэл и Саймон говорят о "колебании(переключении) между целями, требуемыми функциями и средствами, которые необходимы для их выполнения." Здесь, кажется, мы имеем бесконечное колебание между тем, чтобы быть в общеии с магазином (levels 4, 6, 8,...) и попыткой узнать номер телефона магазина (levels 5, 7, 9,...).
Рассуждение заключается в следующем: мы хотим, чтобы магазин узнал о проблеме с батареей, и для этого необходимо с ним(или с ней) проконтактировать.
Один из спопособов проконтактировать, это позвонить по телефону, но у нас нет телефонной книги, чтобы посмотреть номер.
Мы могли бы спросить у них их номер телефона, но для этого нажно с ними проконтактировать.
Как выразился Аристотель, "если мы хотим постоянно размышлять, нам придется дойти до бесконечности". Мы назовем это проблемой "рекусивной подцели": попытке решить проблему в териминах самой проблемы.
Один из способов избежать этой проблемы - заставить achieve отслеживать все цели, над которыми она работает, и прекращать работу, если она найдет петлю в стеке целей.

## 4.10 проблема отсутствия промежуточной информации

Когда GPS не может найт решение, он просто возвращает `nil`.
Это раздражает в тех случаях, когда пользователь ожидал, что решение будет найдено, потому что этот nil не дает никакой информации о причине сбоя.
Пользователь всегда может отследить(трассировать)какую-то функцию, как мы трассировали выше, но вывод трассировки редко является именно той информцией, которая требуется. Было бы неплохо иметь общий инструмент выводящий отладочную информацию, используя который программист мог бы вставлять операторы печати в свой код и выборочно печатать их в зависимости от желаемой информации.

Функция `dbg` обеспечивает эту возможность.
`dbg` печатает выходные данные также как `format`, но она будет печатать только тогда, когда троебуется отладочный вывод.
Каждый вызов `dbg` сопровождается идентификатором, который используется для укзанания класса отладочных сообщений.
Функции `debug` и `undebug` используются для добавления или удаления классов сообщений в список классов, которые должны быть напечатаны.
В этой главе, все выходные отладочные данные будут использовать идентификатор: `gps`.
Другие программы будут использовать другие идентификаторы, а сложная программа будет использовать много идентификаторов.

Вызов `dbg` приведет к выводу, если первый аргумент `dbg`, является тем, который был указан в вызове `debug`.
Остальные аргументы `dbg` - это строка формата, за которой следует список аргументов, которые должны быть напечатаны в соответствии с этой строкой формата.
Другими словами, мы будем писать функции, которые включают в себя вызовы `dbg` такие как:

```lisp
(dbg :gps "The current goal is: ~a" goal)
```
Если мы включили отладку с помощью `(debug :gps)`, то вызовы dbg с идентификатором  :`gps` будут выводить выходные данные.
Вывод выключается с помощью `(undebug :gps)`.
`debug` и `undebug` предназначены для того, чтобы быть похожими на `trace` и `untrace`, в том смысле, что они включают и выключают диагностический вывод.
Они также следуют соглашению, что `debug` без аргументов возвращает текущий список идентификаторов, а `undebug` без аргументов отключает вывод всей отладочной информации.
Однако они в отличии от `trace` и `untrace`, являются функциями, а не макросами.
Если вы исползуете только ключевые слова и целые числа для идентификаторов, то вы не заметите разницы.

Здесь представлены две новые встроенные функции.
Во первых, `*debug-io*` это поток, обычно используемый для отладки ввода/вывода.
Во всех предыдущих вызовах `format` мы использовали t в качестве аргумента потока, что приводило к переходу вывода в поток `*standard-output*`.
Отправка различных типов выходных данных в различные потоки позволяет пользователю проявлять некоторую гибкость.
Например, отладочный вывод может быть направлен в отдельное окно или скопирован в файл.
Во вторых, функция `fresh-line` переходит к следующей строке вывода, если только выходной поток уже не находиться в начале строки.

```lisp
(defvar *dbg-ids* nil "Identifiers used by dbg")

(defun dbg (id format-string &rest args)
  "Print debugging info if (DEBUG ID) has been specified."
  (when (member id *dbg-ids*)
    (fresh-line *debug-io*)
    (apply #'format *debug-io* format-string args)))

(defun debug (&rest ids)
  "Start dbg output on the given ids."
  (setf *dbg-ids* (union ids *dbg-ids*)))

(defun undebug (&rest ids)
 "Stop dbg on the ids. With no ids, stop dbg altogether."
  (setf *dbg-ids* (if (null ids) nil
            (set-difference *dbg-ids* ids))))
```
Иногда легче посмотреть отладочный вывод, если он имеет отступ в соответствии с каким-то шаблоном, например глубиной вложенных вызовов функции.
Для генерации выходных с отступом определяется функция `dbg-indent`:

```lisp
(defun dbg-indent (id indent format-string &rest args)
  "Print indented debugging info if (DEBUG ID) has been specified."
  (when (member id *dbg-ids*)
    (fresh-line *debug-io*)
    (dotimes (i indent) (princ " " *debug-io*))
    (apply #'format *debug-io* format-string args)))
```
## 4.11 GPS Версия 2: более общий решатель проблем

На этом этапе мы готовы собрать воедино новую версию GPS с решениями для проблем "бега вокруг блока", "предварительных условий ломающего цель брата", "прыжка, прежде чем вы увидите проблему" и "рекурсивной подцели".
Глоссарий для новой версии находится в [figure 4.2](#f0015).

| Символ             | Использование                                                     |
| ------             | ---                                                               |
|                    | **Функция верхнего уровняunction**                                |
| `GPS`              | Достигает цели из состояния state используя список операторов     |
|                    | **Специальные переменные**                                        |
| `*ops*`            | Список доступных операторов              .                        |
|                    | **Типы данных**                                                   |
| `op`               | Операция с условиями/состояниями preconds, add-list и del-list.   |
|                    | **Главные функции**                                               |
| `achieve-all`      | достижение списка целей.                                          |
| `achieve`          | достижение индивидуальной цели                                    |
| `appropriate-p`    | Решает, подходит ли оператор для достижения цели.                 |
| `apply-op`         | Применяет оператор к текущему состоянию                           |
|                    | **вспомогательные функции**                                       |
| `executing-p`      | Является ли условие *исполняемой*   формой?                       |
| `starts-with`      | Является ли аргумент списком, который начинается с данного атома? |
| `convert-op`       | Преобраз. оператора при использовании соглашения о *исполнении*.  |
| `op`               | Создает оператор.                                                 |
| `use`              | Используемый список операторов.                                   |
| `member-equal`     | Проверяет, равен ли элемент члену списка.                         |
|                    | **Выбранные Функции Common Lisp**                                 |
| `member`           | Проверяет является ли элемент членом списка (p.78)                |
| `set-difference`   | Все элементы в одном множестве, не входящие в другое              |
| `subsetp`          | Содержится ли одино множество целиком в другом?                   |
| `union`            | Все элементы в любом из двух наборов (p. 62)                      |
| `every`            | проверяет, проходит ли тест каждый элемент списка.                |
| `some`             | Проверяет, проходит ли тест какой-либо элемент списка.            |
| `remove-if`        | Удаляет все элементы, удовлетворяющие тесту.                      |
|                    | **Ранее определенные функции**                                    |
| `find-all`         | Список всех совпадающих элементов (p. 101)                        |
| `find-all-if`      | Список всех элементов, удовлетворяющих предикату                  |

Самое важное изменение заключается в том, что вместо печати сообщения при применении каждого оператора мы будем иметь `GPS`, возвращающий результирующее состояние.
Список "сообщений" в каждом состоянии указывает, какие действия были предприняты.
Каждое сообщение на самом деле является условием, списком формы (executing *operator*).
Это решает проблему "бега вокруг блока": мы можем вызвать `GPS` с начальной целью `((executing run-around-block))`, И он выполнит оператор `run-around-block`, Тем самым удовлетворяя этой цели.
Следующий код определяет новую функцию op, которая строит операторы, включающие сообщение(message) в свой список add-list.

```lisp
(defun executing-p (x)
  "Is x of the form: (executing ...) ?"
  (starts-with x 'executing))

(defun starts-with (list x)
  "Is this a list whose first element is x?"
  (and (consp list) (eql (first list) x)))

(defun convert-op (op)
  "Make op conform to the (EXECUTING op) convention."
  (unless (some #'executing-p (op-add-list op))
    (push (list 'executing (op-action op)) (op-add-list op)))
  op)

(defun op (action &key preconds add-list del-list)
  "Make a new operator that obeys the (EXECUTING op) convention."
  (convert-op
    (make-op :action action :preconds preconds
          :add-list add-list :del-list del-list)))
```
Операторы, построенные op, будут правильными, но мы можем конвертировать существующие операторы, используя `convert-op` напрямую:

```lisp
(mapc #'convert-op *school-ops*)
```

Это пример исследовательского программирования: вместо того, чтобы начинать все сначала, когда мы обнаруживаем ограничение первой версии, мы можем использовать Lisp для изменения существующих структур данных для новой версии программы.

Определение переменной `*ops*` и структуры op точно такие же, как и раньше, а остальная часть программы состоит из пяти функций, которые мы уже видели: `GPS`, `achieve-all`, `achieve`, `appropriate-p` и `apply-op`.
На верхнем уровне функция `GPS` вызывает `achieve-all`, которая возвращает либо nil, либо допустимое состояние.
Из него мы удаляем все атомы, в результате чего остаются только элементы конечного состояния, которые являются списками - другими словами, действиями формы (`executing`*operator*).
Таким образом, значение возвращаемое `GPS` само по себе является списком действий, предпринимаемых для достижения конечного состояния.
`GPS` больше не возвращает `SOLVED`, когда он находит решение, но она по-прежнему подчиняется условию возврата nil в случае неудачи и не-nil для успеха.
В общем, это хорошая идея, чтобы программа возвращала значимое значение, а не печатала это значение, если есть вероятность, что какая-то другая программа когда-нибудь захочет использовать это значение.

```lisp
(defvar *ops* nil "A list of available operators.")

(defstruct op "An operation"
  (action nil) (preconds nil) (add-list nil) (del-list nil))

(defun GPS (state goals &optional (*ops* *ops*))
  "General Problem Solver: from state, achieve goals using *ops*."
  (remove-if #'atom (achieve-all (cons '(start) state) goals nil)))
```

Первое серьезное изменение в версии 2 очевидно из первой строки программы: отсутствует переменная   `*state*`.
Вместо этого программа отслеживает локальные переменные состояния - state.
Это сделано для того, чтобы решить проблему «прыжка, прежде чем посмотреть», как было сказано ранее.
Все функции `achieve`, `achieve-all` и `apply-op` принимают дополнительный аргумент, который является текущим состоянием, и все возвращают новое состояние в качестве своего значения.
Они также должны подчиняются соглашению о возврате nil в случае неудачи.

Таким образом, у нас получается потенциальная двусмысленность: обозначает ли nil, что решение не найдено или допустимое состояние, которое не имеет условий?
Мы разрешаем двусмысленность, принимая соглашение, согласно которому все состояния должны иметь хотя бы одно условие.
Это соглашение обеспечивается функцией GPS.
Вместо вызова (`achieve-all state goals nil`), GPS вызывает `(achieve-all (cons '(start) state) goals nil)`.
Таким образом, даже если пользователь передает GPS нулевое начальное состояние,  та создаст состояние, содержащее `(start)`, передав его в `achieve-all`.
С этого момента нам гарантируется, что ни одно состояние никогда не станет nil, потому что единственная функция, которая создает новое состояние - это `apply-op`, и мы можем видеть, посмотрев на последнюю строку `apply-op`, что она всегда добавляет что-то в возвращаемое состояние.
(Список добавлений - add-list никогда не может быть nil, потому что в противном случае оператор не был бы уместен.
Кроме того, каждый оператор включает условие (executing ...).)

Обратите внимание, что из конечного значения, которое мы возвращаем из GPS,  удалены все атомы, поэтому в конечном итоге мы сообщаем только о выполненных действиях, поскольку они представлены условиями формы (`executing *action*`).
Добавление условия `(start)` в начале также служит для отличия проблемы, которая не может быть решена, от проблемы, которая решается без выполнения каких-либо действий.
Отказ возвращает nil, в то время как решение без выполнения шагов будет, по крайней мере, включать условие `(start)`, если ничего больше требуется.

Функции, которые возвращают nil как признак сбоя и возвращают какое-то полезное значение, иначе известны как *полупредикаты*(*semipredicates*).
Они подвержены ошибкам только в тех случаях, когда nil может быть истолковано как полезное значение.
Будьте осторожны при определении и использовании полупредикатов: (1) Решите, может ли nil когда-либо быть значимым значением.
(2) Убедитесь, что *пользователь* не может повредить программу, указав в качестве значения nil.
В этой программе GPS - единственная функция, которую пользователь должен вызывать, поэтому, как только мы это учли, все готово.
(3) Убедитесь, что *программа* не может передать значение nil.
Мы сделали это, увидев, что в программе было только одно место, где были созданы новые состояния, и что это новое состояние было сформировано путем добавления одноэлементного списка к другому состоянию.
Следуя этой трехэтапной процедуре, мы получаем неформальное доказательство того, что полупредикаты, работающие с состояниями, будут работать правильно.
Такая процедура неформального доказательства является обычным элементом хорошего дизайна программы.

Другое большое изменение в версии 2 - это введение стека целей для решения проблемы рекурсивных подцелей.
Программа отслеживает цели, над которыми она работает, и немедленно терпит неудачу, если цель выступает в качестве подцели.
Эта проверка выполняется во втором предложении/пункте `achieve`.

Функция `achieve-all` пытается достичь каждой из целей по очереди, устанавливая переменную `current-state` как значение, возвращаемое при каждом последующем вызове функции `achieve`.
Если все цели достигаются по очереди, и если все цели все еще сохраняются в конце (что и проверяет `subsetp`), то возвращается конечное состояние; в противном случае функция завершается ошибкой, возвращая nil.

Большая часть работы выполняется с помощью `achieve`, которой передается состояние, условие единственной цели и набор целей, над которыми до сих пор работали.
Если условие уже находится в состоянии, то `achieve` завершается успешно и возвращает состояние.
С другой стороны, если целевое условие уже находится в стеке целей, тогда нет смысла продолжать - мы застрянем в бесконечном цикле, поэтому `achieve` вернет nil.
В противном случае `achieve` просматривает список операторов, пытаясь найти подходящий для применения.

```lisp
(defun achieve-all (state goals goal-stack)
  "Achieve each goal, and make sure they still hold at the end."
  (let ((current-state state))
    (if (and (every #'(lambda (g)
            (setf current-state
              (achieve current-state g goal-stack)))
          goals)
        (subsetp goals current-state :test #'equal))
      current-state)))

(defun achieve (state goal goal-stack)
  "A goal is achieved if it already holds,
  or if there is an appropriate op for it that is applicable."
  (dbg-indent :gps (length goal-stack) "Goal: "a" goal)
  (cond ((member-equal goal state) state)
      ((member-equal goal goal-stack) nil)
      (t (some #'(lambda (op) (apply-op state goal op goal-stack))
          (find-all goal *ops* :test #'appropriate-p)))))
```

Цель `( (executing run-around-block) )` представляет собой список из одного условия, где условие является списком из двух элементов.
Применение списков в качестве условий дает нам большую гибкость, но мы также должны быть осторожны.
Проблема в том, что не все похожие списки на самом деле одинаковы.
Предикат equal по существу проверяет, похожи ли два его аргумента, а предикат `eql` проверяет, идентичны ли два его аргумента.
Поскольку такие функции, как `member` по умолчанию используют `eql`, мы должны указать с помощью ключевого слова `:test`, которое мы хотим вместо него использовать.
Поскольку это делается несколько раз, мы вводим функцию `member-equal`.
Фактически, мы могли бы продвинуть абстракцию еще на один шаг и определить `member-situation`, функцию для проверки того, является ли условие истинным в некоторой ситуации.
Это позволит пользователю изменить функцию сопоставления с `eql` на `equal` и на все остальное, что может быть полезно.


```lisp
(defun member-equal (item list)
  (member item list :test #'equal))
```

Функция `apply-op`, которая раньше безвозвратно изменяла состояние и печатала сообщение, отражающее это, теперь возвращает новое состояние вместо того, чтобы что-либо печатать.
Сначала она вычисляет состояние, которое будет результатом выполнения всех предварительных условий оператора.
Если возможно достичь такого состояния, то `apply-op` возвращает новое состояние, из полученного состояния, добавляя то, что находится в списке добавления(add-list), и удаляя все что находится в списке удаления(delete-list).

```lisp
(defun apply-op (state goal op goal-stack)
  "Return a new, transformed state if op is applicable."
  (dbg-indent :gps (length goal-stack) "Consider: ~a" (op-action op))
  (let ((state2 (achieve-all state (op-preconds op)
            (cons goal goal-stack))))
    (unless (null state2)
      ;; Return an updated state
      (dbg-indent :gps (length goal-stack) "Action: ~a" (op-action op))
      (append (remove-if #'(lambda (x)
            (member-equal x (op-del-list op)))
          state2)
        (op-add-list op)))))

(defun appropriate-p (goal op)
  "An op is appropriate to a goal if it is in its add-list."
  (member-equal goal (op-add-list op)))
```

Есть еще одна сложность в способе вычисления нового состояния.
В версии 1 GPS состояния были (концептуально) неупорядоченными наборами(множествами) условий, поэтому мы могли использовать `union` и `set-difference` для работы с ними.
В версии 2 состояния становятся упорядоченными списками, потому что нам нужно сохранить порядок действий.
Таким образом, мы должны использовать функции `append` и `remove-if`, так как они сохраненяют порядок обрабатываемого списка, а `union` и `set-difference` - нет.

Наконец, последнее отличие версии 2 состоит в том, что она вводит новую функцию: `use`.
Эта функция предназначена для использования в качестве своего рода объявления о том, что данный список операторов должен использоваться для решения ряда проблем.

```lisp
(defun use (oplist)
  "Use oplist as the default list of operators."
  ;; Return something useful, but not too verbose:
  ;; the number of operators.
   (length (setf *ops* oplist)))
```

Вызов use устанавливает параметр `*ops*`, поэтому его не нужно указывать при каждом вызове GPS.
Соответственно, в определении самого GPS третий аргумент, `*ops*`, теперь необязателен; если он не указан, будет использоваться значение по умолчанию.
Значение по умолчанию для `*ops*` дается как `*ops*`.
Это может показаться излишним или черезмерным - как переменная может быть сама собой по умолчанию?
Ответ заключается в том, что два появления `*ops*` похожи друг на друга, но на самом деле они относятся к двум совершенно отдельным привязкам специальной переменной `*ops*`.
В большинстве случаев переменные в списках параметров являются локальными переменными, но нет правила, запрещающего привязку специальной переменной в качестве параметра.
Помните, что эффект привязки специальной переменной заключается в том, что все ссылки на специальную переменную, которые встречаются в любом месте программы - даже за пределами лексической области действия функции, - относятся к новой привязке специальной переменной.
Итак, после последовательности вызовов мы в конечном итоге достигаем достижения, которое ссылается на `*ops*`, и оно увидит новое связанное значение `*ops*`.

Определение GPS повторяемоездесь вместе с альтернативной версией, которая связывает локальную переменную и явно устанавливает и сбрасывает специальную переменную `*ops*`.
Очевидно, что идиома привязки специальной переменной более лаконична, хотя она и может выглядеть вначале запутанной, она полезна, когда ее понимают.

```lisp
(defun GPS (state goals &optional (*ops* *ops*))
  "General Problem Solver: from state, achieve goals using *ops*."
  (remove-if #'atom (achieve-all (cons '(start) state) goals nil)))

(defun GPS (state goals &optional (ops *ops*))
  "General Problem Solver: from state, achieve goals using *ops*."
  (let ((old-ops *ops*))
    (setf *ops* ops)
    (let ((result (remove-if #'atom (achieve-all
                  (cons'(start) state)
                  goals nil ))))
      (setf *ops* old-ops)
      result)))
```

Теперь давайте посмотрим, как работает версия 2.
Мы используем список операторов, который включает в себя оператор "спросить у магазина их номер телефона".
Сначала мы убедимся, что он все еще будет делать примеры, которые сделала Версия 1:

`> (use *school-ops*)`=> `7`

```lisp
> (gps '(son-at-home car-needs-battery have-money have-phone-book)
      '(son-at-school))
((START)
  (EXECUTING LOOK-UP-NUMBER)
  (EXECUTING TELEPHONE-SHOP)
  (EXECUTING TELL-SHOP-PROBLEM)
  (EXECUTING GIVE-SHOP-MONEY)
  (EXECUTING SHOP-INSTALLS-BATTERY)
  (EXECUTING DRIVE-SON-TO-SCHOOL))
```

`> (debug :gps)`=> `(:GPS)`

```lisp
> (gps '(son-at-home car-needs-battery have-money have-phone-book)
      '(son-at-school))
Goal: SON-AT-SCHOOL
Consider: DRIVE-SON-TO-SCHOOL
  Goal: SON-AT-HOME
  Goal: CAR-WORKS
  Consider: SHOP-INSTALLS-BATTERY
    Goal: CAR-NEEDS-BATTERY
    Goal: SHOP-KNOWS-PROBLEM
    Consider: TELL-SHOP-PROBLEM
      Goal: IN-COMMUNICATION-WITH-SHOP
      Consider: TELEPHONE-SHOP
        Goal: KNOW-PHONE-NUMBER
        Consider: ASK-PHONE-NUMBER
          Goal: IN-COMMUNICATION-WITH-SHOP
        Consider: LOOK-UP-NUMBER
          Goal: HAVE-PHONE-BOOK
        Action: LOOK-UP-NUMBER
      Action: TELEPHONE-SHOP
    Action: TELL-SHOP-PROBLEM
    Goal: SHOP-HAS-MONEY
    Consider: GIVE-SHOP-MONEY
      Goal: HAVE-MONEY
    Action: GIVE-SHOP-MONEY
  Action: SHOP-INSTALLS-BATTERY
Action: DRIVE-SON-TO-SCHOOL
((START)
  (EXECUTING LOOK-UP-NUMBER)
  (EXECUTING TELEPHONE-SHOP)
  (EXECUTING TELL-SHOP-PROBLEM)
  (EXECUTING GIVE-SHOP-MONEY)
  (EXECUTING SHOP-INSTALLS-BATTERY)
  (EXECUTING DRIVE-SON-TO-SCHOOL))
```

`> (undebug)`=> `NIL`

```lisp
> (gps '(son-at-home car-works)
      '(son-at-school))
((START)
  (EXECUTING DRIVE-SON-TO-SCHOOL))
```

Теперь мы видим, что версия 2 также может справиться с тремя случаями, в которых Версия 1 ошиблась.
В каждом случае программа избегает бесконечного цикла, а также избегает прыгать до того, как он посмотреть, нет ли какой опасности.

```lisp
> (gps '(son-at-home car-needs-battery have-money have-phone-book)
      '(have-money son-at-school))
NIL
> (gps '(son-at-home car-needs-battery have-money have-phone-book)
      '(son-at-school have-money))
NIL
(gps '(son-at-home car-needs-battery have-money)
      '(son-at-school) )
NIL
```

Наконец, мы видим, что эта версия GPS также работает над тривиальными проблемами, не требующими никаких действий:

`> (gps '(son-at-home) '(son-at-home))`=> `((START))`

## 4.12 новая пробленая область(проблемный домен): обезьяна и бананы

Чтобы показать, что GPS действительно является общим, мы должны заставить его работать в разных областях.
Начнем с "классической" задачи ИИ.[3](#fn0020) Представьте себе следующий сценарий: голодная обезьяна стоит в дверях комнаты.
В центре комнаты на веревке с потолка свисает гроздь бананов, до которых обезьяне не дотянуться.
У двери стоит стул, достаточно легкий, чтобы обезьяна могла толкать его, и достаточно высокий, чтобы дотянуться почти до бананов.
Чтобы все усложнить, предположим, что обезьяна держит игрушечный мяч и может держать только одну вещь одновременно.

Пытаясь представить этот сценарий, мы имеем некоторую гибкость в выборе того, что поместить в текущее состояние и что поместить с операторами.
Теперь предположим, что мы определяем операторы следующим образом:

```lisp
(defparameter *banana-ops*
  (list
    (op
      'climb-on-chair       ;;забраться на стул
      :preconds '(chair-at-middle-room at-middle-room on-floor)
      :add-list '(at-bananas on-chair)
      :del-list '(at-middle-room on-floor))
    (op 
      'push-chair-from-door-to-middle-room ;;толкать стул от двери к середине комнаты
      :preconds '(chair-at-door at-door)
      :add-list '(chair-at-middle-room at-middle-room)
      :del-list '(chair-at-door at-door))
    (op 
      'walk-from-door-to-middle-room ;;идти от двери к середине комнаты
      :preconds '(at-door on-floor)
      :add-list '(at-middle-room)
      :del-list '(at-door))
    (op 
      'grasp-bananas   ;;схавтить банан
      :preconds '(at-bananas empty-handed)
      :add-list '(has-bananas)
      :del-list '(empty-handed))
    (op 
      'drop-ball        ;; бросить мяч
      :preconds '(has-ball)
      :add-list '(empty-handed)
      :del-list '(has-ball))
    (op 
      'eat-bananas      ;;съесть банан
      :preconds '(has-bananas)
      :add-list '(empty-handed not-hungry)
      :del-list '(has-bananas hungry))))
```

Используя эти операторы, мы могли бы поставить задачу стать не-голодными(not-hungry), учитывая начальное состояние нахождения у двери, стоя на полу, держа мяч, голодные и со стулом у двери.
`GPS` может найти решение этой проблемы:

`> (use *banana-ops*)`=> `6`

```lisp
> (GPS '(at-door on-floor has-ball hungry chair-at-door)
      '(not-hungry))
((START)
  (EXECUTING PUSH-CHAIR-FROM-DOOR-TO-MIDDLE-ROOM)
  (EXECUTING CLIMB-ON-CHAIR)
  (EXECUTING DROP-BALL)
  (EXECUTING GRASP-BANANAS)
  (EXECUTING EAT-BANANAS))
```

Обратите внимание, что нам не нужно было вносить какие-либо изменения в программу `GPS`.
Мы просто использовали другой набор операторов.

## 4.13 Проблемная область(домен) Поиск в Лабиринте

Теперь рассмотрим еще одну «классическую» проблему - поиск в лабиринте.
Предположим, что здесь изображен конкретный лабиринт.


![u04-01](images/chapter4/u04-01.jpg)

Намного проще определить некоторые функции, помогающие создавать операторы для этого домена, чем вводить все операторы напрямую.
Следующий код определяет набор операторов для лабиринтов в целом и для этого лабиринта в частности:

```lisp
(defun make-maze-ops (pair)
  "Make maze ops in both directions"
  (list (make-maze-op (first pair) (second pair))
      (make-maze-op (second pair) (first pair))))
(defun make-maze-op (here there)
  "Make an operator to move between two places"
  (op 
    `(move from ,here to ,there)
    :preconds `((at ,here))
    :add-list `((at ,there))
    :del-list `((at ,here))))
(defparameter *maze-ops*
  (mappend #'make-maze-ops
    '((1 2) (2 3) (3 4) (4 9) (9 14) (9 8) (8 7) (7 12) (12 13)
      (12 11) (11 6) (11 16) (16 17) (17 22) (21 22) (22 23)
      (23 18) (23 24) (24 19) (19 20) (20 15) (15 10) (10 5) (20 25))))
```

Обратите внимание на обозначение обратных кавычек ```( ` )```.
Она объясняется в [Разделе 3.2](B9780080571157500030.xhtml#s0020), [page 67](B9780080571157500030.xhtml#p67).

Теперь мы можем использовать этот список операторов для решения нескольких задач с этим лабиринтом.
И мы могли бы легко создать еще один лабиринт, предоставив еще один список связей.
Обратите внимание, что нет ничего, что говорило бы о том, что места в лабиринте расположены по схеме пять на пять - это всего лишь один из способов визуализации связности.

`> (use *maze-ops*)`=> `48`

```lisp
> (gps '((at 1)) '((at 25)))
((START)
  (EXECUTING-(MOVE-FROM-1 TO 2))
  (EXECUTING-(MOVE-FROM-2 TO 3))
  (EXECUTING-(MOVE-FROM-3 TO 4))
  (EXECUTING-(MOVE-FROM-4 TO 9))
  (EXECUTING-(MOVE-FROM-9 TO 8))
  (EXECUTING-(MOVE-FROM-8 TO 7))
  (EXECUTING-(MOVE-FROM-7 TO 12))
  (EXECUTING-(MOVE-FROM-12 TO 11))
  (EXECUTING-(MOVE-FROM-11 TO 16))
  (EXECUTING-(MOVE-FROM-16 TO 17))
  (EXECUTING-(MOVE-FROM-17 TO 22))
  (EXECUTING-(MOVE-FROM-22 TO 23))
  (EXECUTING-(MOVE-FROM-23 TO 24))
  (EXECUTING-(MOVE-FROM-24 TO 19))
  (EXECUTING-(MOVE-FROM-19 TO 20))
  (EXECUTING-(MOVE-FROM-20 TO 25))
  (AT 25))
```

Есть одна тонкая ошибка, на которую указывает домен лабиринта.
Мы хотели, чтобы GPS возвращал список выполненных действий.
Однако, чтобы учесть случай, когда цель может быть достигнута без каких-либо действий, я включил `(START)` в значение, возвращаемое GPS.
Эти примеры включают формы `START` и `EXECUTING`, а также список формы (AT *n*) для некоторых *n*.
Это ошибка.
Если мы вернемся и посмотрим на функцию GPS, мы обнаружим, что она сообщает о результатах, удаляя все атомы из состояния, возвращенного `achieve-all`.
Это «подмена понятий» - мы говорили об удалении атомов, когда на самом деле намеревались удалить все условия, кроме форм `(START)` и `(EXECUTING *action*)`.
До сих пор все эти условия были атомами, поэтому этот подход работал.
В области лабиринта введены условия вида (`AT` *n*), так что впервые возникла проблема.
Мораль такова: когда программист использует подмены, говоря, что удобно, а не то, что на самом деле происходит, неизбежно возникают проблемы.
На самом деле мы хотим не удалять атомы, а найти все элементы, обозначающие действия.
Код ниже говорит о том, что мы имеем в виду:

```lisp
(defun GPS (state goals &optional (*ops* *ops*))
  "General Problem Solver: from state, achieve goals using *ops*."
  (find-all-if #'action-p
        (achieve-all (cons '(start) state) goals nil)))
(defun action-p (x)
  "Is x something that is (start) or (executing ...)?"
  (or (equal x '(start)) (executing-p x)))
```

Домен решений лабиринтов также указывает на преимущество версии 2: она возвращает представление о предпринятых действиях, а не просто распечатывает их.
Причина, по которой это является преимуществом, заключается в том, что мы можем захотеть использовать результаты для чего-то, а не просто смотреть на них.
Предположим, нам нужна функция, которая дает нам путь через лабиринт в виде списка мест, которые нужно посетить по очереди.
Мы могли бы сделать это, вызвав GPS в качестве подфункции, а затем использовать результаты:

```lisp
(defun find-path (start end)
  "Search a maze for a path from start to end."
  (let ((results (GPS `((at ,start)) `((at ,end)))))
    (unless (null results)
      (cons start (mapcar #'destination
              (remove '(start) results
                  :test #'equal))))))
(defun destination (action)
  "Find the Y in (executing (move from X to Y))"
  (fifth (second action)))
```

Функция `find-path` вызывает GPS для получения результатов - `results`.
Если это `nil`, ответа нет, а если есть, то возьмите «остаток» - `rest` от `results` (другими словами, игнорируйте часть `(START)`).
Выберите пункт назначения, `*y*` из каждой формы `(EXECUTING (MOVE FROM x TO y))` и не забудьте указать начальную точку.

`> (use *maze-ops*)`=> `48`

`> (find-path 1 25)`=>

```lisp
(1 2 3 4 9 8 7 12 11 16 17 22 23 24 19 20 25)
```

`> (find-path 1 1)`=> `(1)`

`> (equal (find-path 1 25) (reverse (find-path 25 1)))`=> `T`

## 4.14 Домен Мира Блоков(Blocks World)

Еще одна область(домен), которая привлекает большое внимания в кругах ИИ, - это мир блоков.
Представьте детский набор строительных блоков на столе.
Проблема состоит в том, чтобы переместить блоки из их начальной конфигурации в некоторую целевую конфигурацию.
Мы предполагаем, что каждый блок может иметь только один другой блок непосредственно поверх него, хотя они могут быть размещены на произвольной высоте.
Единственное действие, которое можно предпринять в этом мире, - это переместить отдельный блок, на котором ничего нет, либо наверх другого блока, либо на стол, который представляет мир блоков.
Мы создадим операторы для каждого возможного перемещения блока.

```lisp
(defun make-block-ops (blocks)
  (let ((ops nil))
    (dolist (a blocks)
      (dolist (b blocks)
        (unless (equal a b)
          (dolist (c blocks)
            (unless (or (equal c a) (equal c b))
              (push (move-op a b c) ops)))
          (push (move-op a 'table b) ops)
          (push (move-op a b 'table) ops))))
    ops))
(defun move-op (a b c)
  "Make an operator to move A from B to C."
  (op 
      '(move ,a from ,b to ,c)
      :preconds '((space on ,a) (space on ,c) (,a on ,b))
      :add-list (move-ons a b c)
      :del-list (move-ons a c b)))
(defun move-ons (a b c)
  (if (eq b 'table)
      '((,a on ,c))
      '((.a on ,c) (space on ,b))))
```

Теперь мы опробуем эти операторы на некоторых задачах.
Самая простая проблема - это положить однин блок на другой:


![u04-02](images/chapter4/u04-02.jpg)

`> (use (make-block-ops '(a b)))`=> `4`

```lisp
> (gps '((a on table) (b on table) (space on a) (space on b)
      (space on table))
    '((a on b) (b on table)))
((START)
  (EXECUTING (MOVE A FROM TABLE TO B)))
```

Вот немного более сложная проблема: инвертирование стопки из двух блоков.
На этот раз мы показываем результаты отладки.


![u04-03](images/chapter4/u04-03.jpg)

`> (debug :gps)`=> `(:GPS)`

```lisp
> (gps '((a on b) (b on table) (space on a) (space on table))
      '((b on a)))
Goal: (B ON A)
Consider: (MOVE B FROM TABLE TO A)
  Goal: (SPACE ON B)
  Consider: (MOVE A FROM B TO TABLE)
    Goal: (SPACE ON A)
    Goal: (SPACE ON TABLE)
    Goal: (A ON B)
  Action: (MOVE A FROM B TO TABLE)
  Goal: (SPACE ON A)
  Goal: (B ON TABLE)
Action: (MOVE B FROM TABLE TO A)
((START)
  (EXECUTING (MOVE A FROM B TO TABLE))
  (EXECUTING (MOVE B FROM TABLE TO A)))
```

`> (undebug)`=> `NIL`

Иногда имеет значение, в каком порядке вы пробуете конъюнкты.
Например, вы не можете взять свой торт и съесть его, но вы можете сфотографировать свой торт и тоже съесть его, если вы сделаете снимок *перед* его поеданием.
В мире блоков у нас есть:

![u04-04](images/chapter4/u04-04.jpg)

```lisp
> (use (make-block-ops '(a b c))) 18
> (gps '((a on b) (b on c) (c on table) (space on a) (space on table))
      '((b on a) (c on b)))
((START)
  (EXECUTING (MOVE A FROM B TO TABLE))
  (EXECUTING (MOVE B FROM C TO A))
  (EXECUTING (MOVE C FROM TABLE TO B)))
> (gps '((a on b) (b on c) (c on table) (space on a) (space on table))
      '((c on b) (b on a)))
NIL
```

В первом случае башня была построена путем установки сначала B на A, а затем C на B.
Во втором случае программа сначала получает башню с C на B, но портит эту цель, получая башню с B на A.
Видим ситуацию "предпосылки порчи родственной цели", но программа ничего с этим не делает.
Единственное, что мы могли бы сделать, - это попытаться изменить порядок конъюнктивных целей.
То есть мы могли бы изменить `achieve-all` следующим образом:

```lisp
(defun achieve-all (state goals goal-stack)
  "Achieve each goal, trying several orderings."
  (some #'(lambda (goals) (achieve-each state goals goal-stack))
      (orderings goals)))

(defun achieve-each (state goals goal-stack)
  "Achieve each goal, and make sure they still hold at the end."
  (let ((current-state state))
    (if (and (every #'(lambda (g)
            (setf current-state
              (achieve current-state g goal-stack)))
          goals)
        (subsetp goals current-state :test #'equal))
      current-state)))

(defun orderings (l)
  (if (> (length l) 1)
      (list l (reverse l))
      (list l)))
```

Теперь мы можем представить цель в любом виде, и мы все равно получим ответ.
Заметьте, что мы рассматриваем только два порядка: данный порядок и обратный порядок.
Очевидно, что для множества целей из одного или двух конъюнктов это все упорядочения.
В общем, если есть только одно взаимодействие в множестве целей, то один из этих двух порядков будет работать.
Таким образом, мы исходим из того, что взаимодействия "предпосылки разрушающие родственную цель" редки, и что редко будет больше одного взаимодействия на множестве целей.
Другой возможностью было бы рассмотреть все возможные перестановки целей, но это может занять много времени при большом множестве целей.

Еще одним соображением является эффективность решений.
Рассмотрим простую задачу получения блока С на столе на следующей диаграмме:

![u04-05](images/chapter4/u04-05.jpg)

```lisp
> (gps '((c on a) (a on table) (b on table)
      (space on c) (space on b) (space on table))
    '((c on table)))
((START)
  (EXECUTING (MOVE C FROM A TO B))
  (EXECUTING (MOVE C FROM B TO TABLE)))
```

Решение правильное, но есть более простое решение, которое перемещает C непосредственно на стол.
Более простое решение не было найдено из-за неудачного случая: случается, что `make-block-ops` определяет операторы так, что перемещение C с B на стол происходит раньше, чем перемещение C с A на стол.
Итак, первый оператор опробован, и он успешен при условии, что C находится на B.
Таким образом, двухэтапное решение найдено до того, как будет рассмотрено одноэтапное решение.
В следующем примере выполняется четыре шага, когда это можно было бы сделать за два:

![u04-06](images/chapter4/u04-06.jpg)

```lisp
> (gps '((c on a) (a on table) (b on table)
      (space on c) (space on b) (space on table))
    '((c on table) (a on b)))
((START)
  (EXECUTING (MOVE C FROM A TO B))
  (EXECUTING (MOVE C FROM B TO TABLE))
  (EXECUTING (MOVE A FROM TABLE TO C))
  (EXECUTING (MOVE A FROM C TO B)))
```

Как мы можем найти более короткие решения?
Одним из способов было бы провести полноценный поиск: сначала пробуются более короткие решения, временно забрасываются, когда что-то другое выглядит более перспективным, а затем пересматриваются позже.
Этот подход рассматривается в [главе 6](B9780080571157500066.xhtml), используя общую функцию поиска.
Менее радикальное решение состоит в том, чтобы сделать ограниченную перестановку порядка, в котором ищутся операторы: сначала пробуются те, у которых меньше невыполненных предварительных условий.
В частности, это означает, что операторы со всеми заполненными предварительными условиями всегда будут опробованы раньше других операторов.
Чтобы реализовать этот подход, мы изменим `achieve`:

```lisp
(defun achieve (state goal goal-stack)
  "A goal is achieved if it already holds,
  or if there is an appropriate op for it that is applicable."
  (dbg-indent :gps (length goal-stack) "Goal:~a" goal)
  (cond ((member-equal goal state) state)
      ((member-equal goal goal-stack) nil)
      (t (some #'(lambda (op) (apply-op state goal op goal-stack))
          (appropriate-ops goal state))))) ;***

(defun appropriate-ops (goal state)
  "Return a list of appropriate operators,
  sorted by the number of unfulfilled preconditions."
  (sort (copy-list (find-all goal *ops* :test #'appropriate-p)) #'<
      :key #'(lambda (op)
          (count-if #'(lambda (precond)
              (not (member-equal precond state)))
            (op-preconds op)))))
```

Теперь мы получаем решения, которые хотели:

![u04-07](images/chapter4/u04-07.jpg)

```lisp
> (gps '((c on a) (a on table) (b on table)
      (space on c) (space on b) (space on table))
    '((c on table) (a on b)))
((START)
  (EXECUTING (MOVE C FROM A TO TABLE))
  (EXECUTING (MOVE A FROM TABLE TO B)))
```

![u04-08](images/chapter4/u04-08.jpg)

```lisp
(gps '((a on b) (b on c) (c on table) (space on a) (space on table))
      '((b on a) (c on b)))
((START)
  (EXECUTING (MOVE A FROM B TO TABLE))
  (EXECUTING (MOVE B FROM C TO A))
  (EXECUTING (MOVE C FROM TABLE TO B)))
> (gps '((a on b) (b on c) (c on table) (space on a) (space on table))
      '((c on b) (b on a)))
((START)
  (EXECUTING (MOVE A FROM B TO TABLE))
  (EXECUTING (MOVE B FROM C TO A))
  (EXECUTING (MOVE C FROM TABLE TO B)))
```

### Аномалия Сассмана

Удивительно, но есть проблемы, которые не могут быть решены путем *произвольного* переупорядочения целей.
Рассмотрим:

![u04-09](images/chapter4/u04-09.jpg)

Это выглядит не слишком сложно, так что давайте посмотрим, как наш GPS справляется с этим:

```lisp
> (setf start '((c on a) (a on table) (b on table) (space on c)
        (space on b) (space on table)))
((C ON A) (A ON TABLE) (B ON TABLE) (SPACE ON C)
  (SPACE ON B) (SPACE ON TABLE))
```

`> (gps start '((a on b) (b on c)))`=> `NIL`

`> (gps start '((b on c) (a on b)))`=> `NIL`

Существует проблема "предпосылки повреждающей родственную цель" независимо от того, каким образом мы упорядочиваем конъюнкты!
Другими словами, никакая комбинация планов для двух индивидуальных целей не может решить проблему соединения этих двух целей.
Это удивительный факт, и этот пример стал известен как "аномалия Сассмана".[4] (#fn0025) мы вернемся к этой проблеме в [главе 6](B9780080571157500066.xhtml).

## 4.15 Этап 5 Повторяем: Анализ версии 2

Мы показали, что GPS можно расширять на несколько доменовis extensible to multiple domains.
Главное, что нам не нужно было менять саму программу, чтобы новые домены заработали; мы просто изменили список операторов, переданных в GPS.
Опыт в различных доменах подсказал, какие изменения можно внести, и мы показали, как внести некоторые изменения.
ХОтя версия 2 является большим улучшением по сравнению с версией 1, она по прежнему оставляет желать лучшего.
Теперь мы можем обнаружить несколько наиболее тревожных проблем.

## 4.16 отсутствие присмотра за вами не является проблемой прыжка

Мы решили проблему "прыжка, прежде чем посмотреть, безопасен ли он", введя переменные для хранения представления возможных будущих состояний, а не только одну переменную, представляющую текущее состояние.
Это не позволяет GPS предпринять опрометчивые действия, но мы увидим, что даже со всеми стратегиями восстановления, представленными в последнем разделе, это не гарантирует, что решение будет найдено всякий раз, когда оно возможно.

Чтобы увидеть проблему, добавьте еще один оператор в начало списка `*school-ops*` и снова включите вывод отладочной информации:

```lisp
(use (push (op 'taxi-son-to-school
        :preconds '(son-at-home have-money)
        :add-list '(son-at-school)
        :del-list '(son-at-home have-money))
      *school-ops*))
(debug :gps)
```

Теперь рассмотрим проблему получения ситуации: ребенок в школе без использования денег:

```lisp
> (gps '(son-at-home have-money car-works)
      '(son-at-school have-money))
Goal: SON-AT-SCHOOL
Consider: TAXI-SON-TO-SCHOOL
  Goal: SON-AT-HOME
  Goal: HAVE-MONEY
Action: TAXI-SON-TO-SCHOOL
Goal: HAVE-MONEY
Goal: HAVE-MONEY
Goal: SON-AT-SCHOOL
Consider: TAXI-SON-TO-SCHOOL
  Goal: SON-AT-HOME
  Goal: HAVE-MONEY
Action: TAXI-SON-TO-SCHOOL
NIL
```

Первые пять строк выходных данных успепшно решают проблему: сын в школе - `son-at-school` с помщью действия `TAXI-SON-TO-SCHOOL`.
Следующая строка показывает безуспешную попытку достичь цели: иметь деньги - `have-money`.
Следующий шаг - попробовать в другом порядке.
На этот раз сначала делается попытка получить деньги, она оказывается успешной.
Затем, цель `son-at-school` снова достигается благодаря действию `TAXI-SON-TO-SCHOOL`.
Но проверка на согласованность в  `achieve-each`   не удается, и в этом месте исправление не доступно.
Цель не достигется, хотя есть верное решине: отвезти ребенка в школу.

Проблема в том, что   `achieve`   использует  `some`  для просмотра(поиска) применимых операторов - `appropriate-ops`.
Таким образом, если есть какой-то подходящий оператор, `achieve` завершится успешно.
Если есть только одна цель, это даст правильное решение.
Однако, если есть несколько подцелей, как в этом случае, achieve все равно найдет только один способ достич первой цели.
Если первое решение плохое, единственный выход - попытаться его исправить.
В таких областях(доменах), как мир блоков и мир лабиринтов, исправление часто работает, потому что все шаги обратимы.
Но в примере с такси никакое исправление плана не может вернуть деньги после того, как они были потрачены, поэтому весь план терпит неудачу.

Есть два пути обойти эту проблему.
Первый подход - изучить все возможные решения, а не только первое решение, которое позволяет достичь каждой подцели.
Язык Prolog, который будет обсуждаться в [chapter 11](B978008057115750011X.xhtml), именно этим и занимается.
Второй подход состоит в том, чтобы achieve и `achieve-all` отслеживали список целей, которые необходимо *защищать/protected*.
В примере с такси мы бы тривиально достигли цели `have-money`, а затем попытались бы достичь цели `son-at-school`, защищая цель `have-money`.
Оператор будет уместен только в том случае, если он не удаляет никаких защищенных целей.
Этот подход по прежнему требует какого то исправления или поиска по нескольким путям решения.
Если бы мы попробовали только один порядок - достигающий `son-at-school`, а затем попытались бы защитить его, достигая при этом `have-money`- то мы бы не нашли решения.
Планировщик  WARPLAN Дэвида Уоррена хорошо использует идею защищенных целей.

## 4.17 проблема отсутствия описательной мощности

В домене лабиринтов было бы намного экономичнее иметь один оператор, который говорил бы, что мы можем перемещаться отсюда/here туда/there, если мы находимся "здесь/here" и если существует связь "отсюда/here" к "туда/there". Затем входные данные для конкретной задачи могут содержать список допустимых соединений и мы могли бы решить любой лабиринт с помощью этого единственного оператора.
Точно так же мы определили оператор, в котором обезьяна толкает стул от двери к середине комнаты, но было бы лучше иметь оператор, с помощью которого обезьяна могла бы толкать стул из любого места в любое другое близлежащее место, или еще лучше - оператор которые толкает любой "толкаемый" объект из одного места в соседнее, если нет никаких препятствий.
Вывод таков: мы хотели бы иметь переменные в операторах, чтобы можно было сказать что-то вроде:

```lisp
(op 
  '(push X from A to B)
  :preconds '((monkey at A) (X at A) (pushable X) (path A B))
  :add-list '((monkey at B) (X at B))
  :del-list '((monkey at A) (X at A)))
```

Часто мы хотим охарактеризовать состояния в терминах чего-то более абстрактного, чем список условий.
Например, при решении шахматной задачи цель состоит в том, чтобы поставить опоненту мат, ситуацию которую нельзя экономно описать в терминах таких примитивов, как `(black king on A 4)`, поэтому мы должны иметь возможность сформулировать какой-то вид ограничений на состояние цели, а не просто перечисления её компонентов.
Мы могли бы захотеть достичь дизъюнкции(ИЛИ) или отритцания условий, тогда как текущий формализм допускает только коньюнкцию(И).

Во многих областях(доменах) также важно уметь формулировать проблемы, связанные со временем: мы хотим достичь *X* до наступления времени *T0*, и затем достичь *Y* до наступления времени *T2*, но не раньше *T1*.
Планирование работы в производственном цехе или строительство дома - примеры планирования, в котором время играет важную роль.

Часто действия связаны с расходами и мы хотим найти решение с минимальными или почти минимальными затратами.
Стоимость может быть такой же простой, как количество операторов, необходимых для решения - мы видели в домене мира блоков, что иногда оператор, который можно было применить немедленно, игнорировался, и вместо него выбирался оператор, которому требовалолсь выполнение нескольких предварительных условий.
Или нас могло бы удовлетворить частичное решение, если полное решение невозможно или слишком дорого. 
Мы также можем принимать во внимание стоимость (или время) вычислений.

## 4.18 Идеальная Информационная Проблема

Все опереторы, которые мы видели до сих пор, дают однозначные результаты; они добавляют или удаляют определенные вещи из текущего состояния и  GPS всегда точно знает, что с ними делать.
В реальном мире, вещи редко бывают такими простыми.
Возвращаясь к проблеме обогащения, можно сказать, что в лотерею играет один соответствующий оператор.
Этот оператор использует несколько долларов и время от времени выплачивает крупную сумму.
Но у нас нет способа представить выигрыш "только от времени". Точно так же у нас нет возможности представлять неожиданные трудности любого рода.
В задаче про детский сад мы могли бы представить проблему с автомобильным аккумулятором, если бы GPS явно проверял, работает автомобиль или ему нужен аккумулятор, каждый раз, когда программа обдумывает оператор перемещения/driving.
В реальном мире мы редко бываем так осторожны; мы садимся в машину и только тогда когда она не заводится, мы рассматриваем возможность разрядки аккумулятора.

## 4.19 Проблема Взаимодействующих Целей

Люди как правило преследуют несколько целей, а не работают только над одной.
Я не только хочу отвести ребенка в сад, но я хочу, чтобы меня не сбила машина, чтобы успеть вовремя на работу, встретиться с друзьями, повеселиться, продолжить дышать и т.д.
Я также должен самостоятельно определять цели, а не работать над набором заранее определенных целей, переданных мне кем-то.
Некоторые цели я могу годами держать в тени, а затем работать над ними, когда представиться возможность.
Никогда не бывает идеи достижения всех возможных целей.
Скорее, это непрерывный процесс достижения одних целей, частичного достижения других и откладывания или отказа от третьих.

Помимо активных целей, люди также оседомлены о нежелательных ситуациях, которых они пытаются избежать.
Например, предположим, что у меня есть цель навести друга в больнице.
Для этого мне нужно идти в больницу.
Один применимый оператор может: идти в больницу, в то время как другой может: пораниться и ждать пока скорая помощь не отвезет меня туда.
Второй оператор достигает цели так же хорошо(а возможно и быстрее), но имеет нежелательный побочный эффект.
Это можно решить либо с помощью понятия стоимости(цены) решения,  как указано в последнем разделе, либо с помощью списка фоновых целей, которые пытаются защитить каждое решение.

Herb Simon ввел термин "удовлетворительный", чтобы описать стратигию достижения разумного числа целей в разумной степени при отказе от других целей или откладывании их на потом.
GPS знает только успехи или неудачи, и поэтому не может максимизировать частичный успех.

## 4.20 Конец GPS

Эти последние четыре раздела дают представление об ограничениях  GPS.
Фактически, это вообще не средство решения общих проблем.
Он *является* общим в том смысле, что алгоритм не привязан к определенной области(домену); мы можем изменить домен, изменяя операторы.
Но GPS не может быть универсальным, поскольку не может решить многие интересные проблемы.
Он сводиться к маленьким фокусам и играм.

Есть важная, но тонкая причина, по которой GPS была обречена на провал, причина, которая не получила признания в 1957, но сейчас она лежит в основе компьютерных наук.
Сейчас признано, что есть проблемы, которые компьютеры не могут решить - не потому, что теоретически правильная программа не может быть написана, а потому, что выполнение программы займет слишком много времени.
Можно показать, что большое количество проблем попадает в класс "NP-сложных" проблем.
Вычисление решения этих проблем требует времени, которое экспотенциально растет по мере увеличения размера проблемы.
Это свойство самих проблем, и оно выполняется каким бы умным не был программист.
Экспотенциальный рост означает, что проблемы, которые могут быть решены за секунды, например для случая с пятью входами, могут занять триллионы лет, когда есть 100 входов.
Покупка более быстрого компьютера не сильно поможет.
В конце концов, если на решение проблемы на вашем компьютере потребуется триллион лет, покупка 1000 компьютеров каждый в 1000 раз быстрее вашего, не поможет: вам все равно придется ждать миллион лет.
Для теоретика в области информатики обнаружение NP-трудности проблемы является самоцелью.
Но для работника AI, это означает, что задается не тот вопрос.
Многие проблемы являются  NP-трудными, когда мы настаиваем на оптимальном решении, которое может быть не лучшим.

Входные данные для  GPS - это по сути программа, а выполнение GPS - это выполнение этой программы.
Если язык ввода GPS является достаточно общим, чтобы выразить любую программу, тогда возникнут проблемы, которые невозможно решить, либо потому, что они выполняются слишком долго, либо потому, что у нех нет решения.
Современные программы решения проблем признают это фундаментальное ограничение и либо ограничивают класс проблем, которые они пытаются решить, либо рассматривают способы поиска приблизительных или частичных решений.
Некоторые специалисты по решению проблем также контролируют собственное время выполнения и знают достаточно, чтобы отказаться, когда проблема слишком сложна.

Следующая цитата из статьи Дрю МакДермотта "Искуственный интеллект встречается с естественной глупостю" резюмирует нынешние представления о GPS.
Помните об том в следующий раз, когда вам нужно будет назвать программу.

> *Помните GPS?
К настоящему времени "GPS" - это бесцветный термин, означающий особо глупую программу для решения головоломок.
Но изначально оно означало "Решатель Общих Проблем", что вызывало у всех ненужное волнение и смятение.
Она должна была называться  *lfgns* - "Локальный поисковик в сети указанных характеристик."

Тем не менее, GPS оказался полезным средством изучения программирования в целом и программирования AI в частности.
Что еще более важно, это был полезный инструмент для исследования "природы размышлений". Конечно, мы признаем, что Аристотель был более умным человеком, чем вы или я, но с помощью вычислительной модели разума в качестве ведущей метафоры и дальнейшей помощи рабочей компьютерной программы, помогающей исследовать эту метафору, мы были ведомы к более тщательному пониманию анализа средств и целей - по крайней мере, в рамках вычислительной модели.
Мы должны сопротивляться искушению поверить в то, что все мышление следует этой модели.

Привлекательность AI можно рассматривать как разделение между средствами и целями.
Завершением успешного проекта AI может быть программа, которая выполняет некоторые полезные задачи лучше, быстрее или дешевле, чем это могло быть ранее.
По этим меркам GPS в большинстве случаев является ошибкой, поскольку он не решает многие проблемы особенно хорошо.
Но средства для достижения этой цели включали исследование и формализацию процесса решения проблем.
Таким образом, наша реконструкция GPS является успешной в той мере, в какой она помогает читателю лучше понять суть проблемы.

## 4.21 история и ссылки

Оригинальный GPS задокументирован в статье Ньюэлла и Саймона 1963 года и в их книге 1972 года "решение человеческих проблем", а также В книге Эрнста и Ньюэлла 1969 года.
Реализация в этой главе основана на программе Stripes (Fikes and Nilsson 1971).

Есть и другие важные программы планирования.
Программа Эрла Сакердоти Abstrips представляла собой модификацию Strips, которая позволяла осуществлять иерархическое планирование.
Идея состояла в том, чтобы набросать скелетный план, который решает всю программу на абстрактном уровне, а затем заполнить детали.
Планировщик  Warplan Дэвида Уоррена освещен в Warren 1974a, b и в разделе Coelho and Cotta 1988.
Система Nonlin  Остина Тейта (Tate 1977) достигла большей эффективности, рассматривая план как частично упорядоченную последовательность операций, а не как строго упорядоченную последовательность ситуаций.
Tweak Дэвида Чэпмена синтезирует и формализует современное состояние планирования по состоянию на 1987 год.

Все эти документы-и довольно много других важных документов по планированию-перепечатаны в Allen, Hendler, and Tate 1990.

## 4.22 Упражнения

**Exercise  4.1 [m]** Можно реализовать dbg с помощью одного вызова format.
Можете ли вы определить директивы format, чтобы сделать это?

**Exercise  4.2 [m]** Напишите функцию, которая генерирует все перестановки ее входных данных..

**Exercise  4.3 [h]** GPS не распознает ситуацию, когда цель случайно достигается как часть достижения другой цели.
Рассмотрим цель - поедание десерта.
Предположим, что есть два доступных оператора: съесть мороженое (которая требует наличия мороженого) и съесть торт (который требует наличия торта).
Предположим, что мы можем купить торт, и что пекарня проводит акцию, когда она выдает бесплатное мороженое каждому клиенту, который покупает и ест торт.
(1) Составьте список операторов для представления этой ситуации.
(2) Дайте gps цель съесть десерт.
Покажите, что, имея правильный список операторов, `gps` решит съесть мороженое, затем решит купить и съесть торт, чтобы получить бесплатное мороженое, а затем пойти вперед и съесть мороженое, даже если цель съесть десерт уже достигнута, поеданием торта.
(3) исправьте gps так, чтобы он не проявлял этой проблемы.

Следующие упражнения направлены на решение проблем в версии 2 программы.

**Exercise  4.4 [h]** *То, что вы не смотрите за собой, не является проблемой прыжка*.
Напишите программу, которая отслеживает оставшиеся цели, чтобы она не застряла, рассматривая только одну возможную операцию, когда другие в конечном итоге приведут к цели.
Подсказка: пусть achieve примет дополнительный аргумент, указывающий на цели, которые остаются достижимыми после достижения текущей цели.
`achieve` должно быть успешным только в том случае, если оно может достичь текущей цели, а также `achieve-all` остальных целей.

**Exercise  4.5 [d]** Напишите программу планирования, которая, как и Warplan Уоррена, отслеживает список целей, которые еще предстоит достичь, а также список целей, которые были достигнуты и не должны быть отменены.
Программа никогда не должна отменять цель, которая была достигнута, но она должна допускать возможность переупорядочения шагов, которые уже были предприняты.
Таким образом, программа решит аномалию Сассмана и подобные проблемы.

**Exercise  4.6 [d]** *Отсутствие описательной проблемы власти*.
Прочитайте [главы 5](B9780080571157500054.xhtml) и [6](B9780080571157500066.xhtml), чтобы узнать о сопоставлении шаблонов.
Напишите версию GPS, которая использует инструменты сопоставления образцов и, таким образом, допускает переменные в операторах.
Применяйте его к доменам лабиринт и миру блоков.
Ваша программа будет более эффективной, если, подобно программе Чепмена Tweak, вы допускаете возможность переменных, которые остаются несвязанными как можно дольше.

**Exercise  4.7 [d]** Поразмышляйте над дизайном планировщика, который может решить  проблемы *идеальной информации* и *взаимодействующих целей*.

## 4.23 Ответы

**Answer 4.1** В этой версии форматная строка `"~&~V@T~?"` разбивается следующим образом: `"~&"` означает перейти к новой строке;`"~V@T"` означает вставить `(@T)` пробелы, но используйте следующий аргумент `(V)`, чтобы получить количество пробелов.
Это `"~?"` - это оператор косвенности: используйте следующий аргумент в качестве строки формата, а следующий за ним аргумент - в качестве списка аргументов для строки формата.

```lisp
(defun dbg-indent (id indent format-string &rest args)
  "Print indented debugging info if (DEBUG ID) has been specified."
  (when (member id *dbg-ids*)
    (format *debug-io* "~&~V@T~?" (* 2 indent) format-string args)))
```

**Answer 4.2** Вот одно из решений.
Искушенный программист Lisp также должен видеть это упражнение на [page 680](B9780080571157500194.xhtml#p680).

```lisp
(defun permutations (bag)
  "Return a list of all the permutations of the input."
  ;; If the input is nil, there is only one permutation:
  ;; nil itself
  (if (null bag)
      '(())
      ;; Otherwise, take an element, e, out of the bag
      ;; Generate all permutations of the remaining elements,
      ;; And add e to the front of each of these.
      ;; Do this for all possible e to generate all permutations,
      (mapcan #'(lambda (e)
          (mapcar #'(lambda (p) (cons e p))
            (permutations
              (remove e bag :count 1 :test #'eq))))
        bag)))
```

----------------------

[1](#xfn0010)Strips is the Stanford Research Institute Problem Solver, designed by [Richard Fikes and Nils Nilsson (1971)](B9780080571157500285.xhtml#bb0405).

[2](#xfn0015) Gerald Sussman, in his book *A Computer Model of Skill Acquisition,* uses the term "prerequisite clobbers brother goal" or PCBG.
I prefer to be gender neutral, even at the risk of being labeled a historical revisionist.

[3](#xfn0020) Originally posed by [Saul Amarel (1968)](B9780080571157500285.xhtml#bb0045).

[4](#xfn0025) A footnote in Waldinger 1977 says, "This problem was proposed by Allen Brown.
Perhaps many children thought of it earlier but did not recognize that it was hard." The problem is named after Gerald Sussman because he popularized it in Sussman 1973.